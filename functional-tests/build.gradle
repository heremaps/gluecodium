ext {
    ndkVersion = '21.4.7075529'
    compileSdkVersion = 28
    targetSdkVersion = 28
    minSdkVersion = 24
    cmakeVersion = '3.14.0+'
    buildToolsVersion = '29.0.2'

    buildRoot = "${rootProject.projectDir}/build-android"
    buildNativeRoot = "${buildRoot}/cpp"
    generatedDir = "${buildRoot}/generated"

    rootProject.buildDir = "${buildRoot}/gradle"

    isHostOnly = {
        return project.hasProperty('hostOnly')
    }

    useGluecodiumVersion = {
        if (project.hasProperty('gluecodiumVersion')) {
            return project.property('gluecodiumVersion').toString().trim()
        }
        return '+'
    }

    buildNativeHostDir = { projectName, variantName ->
        return "${buildNativeRoot}/${projectName}/cmake/${variantName}/host/"
    }
}

public class BuildCMakeNativeHost extends DefaultTask {
    @Input
    File buildDir

    @Input
    File projectDir

    @Input
    List<String> cmakeParameters

    @Input
    String target

    @Input
    String buildType

    BuildCMakeNativeHost() {
        doFirst {
            project.mkdir buildDir
        }

        doLast {
            project.exec {
                List<String> cmakeCommandLine = ['cmake', projectDir.toString(), '-G', 'Ninja'] + cmakeParameters
                commandLine(*cmakeCommandLine)
                workingDir buildDir
            }

            project.exec {
                commandLine 'cmake',
                    '--build', buildDir.toString(),
                    '--target', target,
                    '--config', buildType
            }
        }
    }
}

ext.BuildCMakeNativeHost = BuildCMakeNativeHost

buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.32"

        def gluecodiumVersion = project.hasProperty('gluecodiumVersion')
            ? project.property('gluecodiumVersion').toString().trim() : '+'

        classpath "com.here.gluecodium:gluecodium-gradle:${gluecodiumVersion}"
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.10.2'

        modules {
            module("org.jetbrains.trove4j:trove4j") {
                replacedBy("org.jetbrains.intellij.deps:trove4j")
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
