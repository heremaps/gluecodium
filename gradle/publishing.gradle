/*
 * Copyright (C) 2016-2024 HERE Europe B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 * License-Filename: LICENSE
 */

apply plugin: 'maven-publish'

Properties properties = new Properties()
properties.load(project.rootProject.file('gluecodium/src/main/resources/version.properties').newDataInputStream())
version = properties.getProperty('version')
group = 'com.here.gluecodium'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing.publications {
    mavenJava(MavenPublication) {
        groupId
        artifactId project.name
        version this.version

        from components.java
        artifact sourcesJar
        artifact javadocJar

        pom {
            name = project.ext.longName
            description = 'Cross-language bindings generator'
            url = 'https://github.com/heremaps/gluecodium'
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection = 'scm:git:https://github.com/heremaps/gluecodium.git'
                developerConnection = 'scm:git:git@github.com:heremaps/gluecodium.git'
                url = 'https://github.com/heremaps/gluecodium'
            }
        }
    }
}

apply plugin: 'com.jfrog.artifactory'

artifactory {
    contextUrl = project.properties['artifactory.contextUrl']
    publish {
        repository {
            username = project.properties['artifactory.username']
            password = project.properties['artifactory.password']
            repoKey = project.properties['artifactory.repoKey']
        }
        defaults {
            publications 'mavenJava'
        }
    }
}
