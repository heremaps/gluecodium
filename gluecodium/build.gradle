/*
 * Copyright (C) 2016-2019 HERE Europe B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 * License-Filename: LICENSE
 */

plugins {
    id "com.lazan.dependency-export" version "0.5"
}
dependencies {
    compile project(":lime-runtime")
    compile project(":lime-loader")

    compile 'com.android.tools.build:manifest-merger:25.3.0'
    compile 'com.android.tools:annotations:25.3.0'
    compile 'com.google.guava:guava:29.0-jre'
    compile 'com.natpryce:konfig:1.6.10.0'
    compile 'com.vladsch.flexmark:flexmark-util:0.62.2'
    compile 'com.vladsch.flexmark:flexmark:0.62.2'
    compile 'commons-cli:commons-cli:1.4'
    compile 'commons-io:commons-io:2.7'
    compile 'org.apache.logging.log4j:log4j-core:2.13.3'
    compile 'org.apache.commons:commons-text:1.8'
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.4.20'
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.4.20'
    compile 'org.jetbrains:annotations:13.0'
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.slf4j:slf4j-log4j12:1.7.26'
    compile 'org.trimou:trimou-core:2.5.0.Final'

    testCompile files({ project(":lime-runtime").sourceSets.test.output })
    testCompile 'io.mockk:mockk-dsl-jvm:1.10.0'
    testCompile 'io.mockk:mockk:1.10.0'
    testCompile 'junit:junit:4.12'
}

apply plugin: 'application'
mainClassName = 'com.here.gluecodium.Gluecodium'

jar {
    manifest {
        attributes('Main-Class': 'com.here.gluecodium.Gluecodium',
                   'Class-Path': configurations.runtime.files.collect { it.getName() }.join(' '))
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = '0.8.4'
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            limit {
                counter = 'LINE'
                minimum = 0.90
            }
        }
    }
}
jacocoTestReport {
    reports {
        csv.enabled true
        html.enabled true
        xml.enabled true

        csv.destination file("${buildDir}/reports/coverage/coverageReport.csv")
        html.destination file("${buildDir}/reports/coverage/html")
        xml.destination file("${buildDir}/reports/coverage/coverageReport.xml")
    }
}

apply from: "$rootDir/gradle/spotless.gradle"
spotless {
    format 'cpp', {
        target project.fileTree(project.projectDir) {
            include '**/*.h', '**/*.cpp'
            exclude '**/build', '**/test/resources'
        }
        licenseHeaderFile rootProject.file('config/spotless/here_cpp.license'),
                '(#pragma|#include)'
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'swift', {
        target project.fileTree(project.projectDir) {
            include '**/*.swift'
            exclude '**/build', '**/test/resources'
        }
        licenseHeaderFile rootProject.file('config/spotless/here_cpp.license'),
                '(import|class|public class|protocol|/// :nodoc:)'
        trimTrailingWhitespace()
        endWithNewline()
    }
}

project.ext.longName = 'Gluecodium command-line tool'
apply from: "$rootDir/gradle/publishing.gradle"

apply plugin: "com.lazan.dependency-export"
mavenDependencyExport {
    configuration 'runtime'
}
