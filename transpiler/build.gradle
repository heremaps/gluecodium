plugins {
    id 'com.diffplug.gradle.spotless' version '3.3.2'
}

dependencies {
    compile 'commons-io:commons-io:2.5'
    compile 'org.eclipse.core:runtime:3.10.0-v20140318-2214'
    compile 'org.eclipse.xtext:org.eclipse.xtext.generator:2.11.0'
    compile 'org.franca:core:0.11.1.201705031527'
    compile 'org.franca:core-dsl:0.11.1.201705031527'
    compile 'org.franca:deploymodel-dsl:0.11.1.201705031527'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-jdk14:1.7.25'
    compile 'org.trimou:trimou-core:2.2.0.Final'

    compileOnly 'org.projectlombok:lombok:1.16.18'

    testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.9.0'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0'
    testCompile 'org.powermock:powermock-classloading-xstream:1.7.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.7.0'
}

apply plugin: 'application'
mainClassName = 'com.here.ivi.api.Transpiler'
run {
    /* By default gradle doesn't forward the command line arguments to main.
     This allows to provide a -Dexec.args="args" when the user calls gradle
     :transpiler:run. This way args will map to the args in main. */
    if (System.getProperty('exec.args') != null) {
        args System.getProperty('exec.args').split()
    }
}

jar {
    manifest {
        attributes ('Main-Class': 'com.here.ivi.api.Transpiler',
                    'Class-Path': configurations.runtime.files.collect { it.getName() }.join(' '))
    }
}

task transpilerStandaloneJar(type: Jar) {
    baseName = project.name + '-standalone'
    manifest {
        attributes('Main-Class': 'com.here.ivi.api.Transpiler',
                   'Class-Path': configurations.runtime.files.collect { it.getName() }.join(' '))
    }
    from {
        configurations.runtime.files.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
    with jar
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = '0.7.9'
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            limit {
                counter = 'LINE'
                minimum = 0.75
            }
        }
    }
}
jacocoTestReport {
    reports {
        csv.enabled true
        html.enabled true
        xml.enabled true

        csv.destination file("${buildDir}/reports/coverage/coverageReport.csv")
        html.destination file("${buildDir}/reports/coverage/html")
        xml.destination file("${buildDir}/reports/coverage/coverageReport.xml")
    }
}

apply plugin: 'checkstyle'
checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle-hard.xml')
    toolVersion = '7.8.2'
    ignoreFailures false
}
checkstyleMain {
    maxErrors=0
    maxWarnings=0
}
checkstyleTest {
    maxErrors=0
    maxWarnings=0
}

apply plugin: 'pmd'
pmd {
    ruleSetFiles = files(rootProject.file('config/pmd/pmd-hard.xml'))
    rulePriority = 3
}

apply from: "$rootDir/gradle/publish.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
