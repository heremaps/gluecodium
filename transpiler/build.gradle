plugins {
    id "org.xtext.xtend" version "1.0.17"
}

apply plugin: 'jacoco'

xtend {
    // generate xtend debug symbols
    debugger {
        sourceInstaller = 'SMAP'
        hideSyntheticVariables = true
    }
}


configurations {
    buildDeployModel {
        extendsFrom compile
    }
}

ext {
    fdeplOutput = "${buildDir}/fdepl-gen/"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', project.fdeplOutput
        }
        resources {
            srcDirs "src/main/java"
            include "**/*.fdepl"
            include "**/*.fidl"
            include "**/*.h"
            include "**/*.cpp"
            include "**/*.properties"
        }
    }
    test {
        java {
        }
    }
}

dependencies {
    buildDeployModel project(':fdeplgen')

    compile 'commons-io:commons-io:+' // apache commons io for FileUtils
    compile 'org.apache.commons:commons-lang3:3.+'
    compile project(':externals:core')
    compile project(':externals:dsl')
    compile project(':externals:deploymodel')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.eclipse.xtext:org.eclipse.xtext.junit4:2.9.2'
    testCompile 'org.eclipse.xtext:org.eclipse.xtext.xbase.junit:2.9.2'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
}

def buildDeployModelConfiguration() {
    return {
        classpath = configurations.buildDeployModel
        main = 'com.here.ivi.api.FrancaDeployModelGenerator'
        workingDir = 'src/main/resources'
        args += project.fdeplOutput
        args += 'franca/spec/BaseApiSpec.fdepl'
    }
}
task buildDeployModel(type: JavaExec) {
    configure buildDeployModelConfiguration()
}
generateXtext.dependsOn(buildDeployModel)

apply plugin: 'application'
mainClassName = 'com.here.ivi.api.Transpiler'

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            limit {
                counter = 'LINE'
                minimum = 0.40
            }
        }
    }
}

jacocoTestReport {
    reports {
        csv.enabled true
        html.enabled true
        xml.enabled true

        csv.destination file("${buildDir}/reports/coverage/coverageReport.csv")
        html.destination file("${buildDir}/reports/coverage/html")
        xml.destination file("${buildDir}/reports/coverage/coverageReport.xml")
    }
}

jar {
    manifest {
        attributes ('Main-Class': 'com.here.ivi.api.Transpiler',
                    'Class-Path': configurations.runtime.files.collect { it.getName() }.join(' '))
    }
}

task transpilerStandaloneJar(type: Jar) {
    baseName = project.name + '-standalone'
    manifest {
        attributes('Main-Class': 'com.here.ivi.api.Transpiler',
                   'Class-Path': configurations.runtime.files.collect { it.getName() }.join(' '))
    }
    from {
        configurations.runtime.files.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

run {
    /* By default gradle doesn't forward the command line arguments to main.
     This allows to provide a -Dexec.args="args" when the user calls gradle
     :transpiler:run. This way args will map to the args in main. */
    if (System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    } else {
        args "../fidl"
    }
}

task diff(type: JavaExec) {
     classpath=sourceSets.main.runtimeClasspath
     main = 'com.here.ivi.api.Fidldif'
     if (System.getProperty("exec.args") != null) {
       args System.getProperty("exec.args").split()
     }
     else {
       args 'test/data/Runtime.old.fidl', 'test/data/Runtime.fidl'
     }
}

apply plugin: 'checkstyle'
checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle-hard.xml')
    toolVersion = "7.8.2"
    ignoreFailures true
}

apply plugin: 'pmd'
pmd {
    ruleSetFiles = files(rootProject.file('config/pmd/pmd-hard.xml'))
    ignoreFailures true
}

apply from: "$rootDir/gradle/publish.gradle"
