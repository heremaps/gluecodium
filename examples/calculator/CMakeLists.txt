# Copyright (C) 2016-2023 HERE Europe B.V.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0
# License-Filename: LICENSE

cmake_minimum_required(VERSION 3.12)

project(gluecodium.calculator)

option(ENABLE_APP "Enables iOS app which demostrates how to use the generated code" ON)

set(CMAKE_CXX_STANDARD 17)

if(CMAKE_GENERATOR STREQUAL "Xcode")
  enable_language(Swift)
endif()

# Path to Gluecodium CMake wrapper
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/../../cmake/modules")

include(gluecodium/Gluecodium)

# Android requires C++ code to be built as shared library.
# For iOS it's recommended (but not required) to do Framework because it's consistent
# to Android setup and simplifies CMake scripts and it allows to use framework with
# Xcode projects which are not originated from CMake (for example cocoapods or
# generated by Xcode itself).
add_library(mylibrary SHARED "${CMAKE_CURRENT_LIST_DIR}/cpp/CalculatorImpl.cpp")

# C++ (cpp) generator usually always necessary, android and swift depend on current platform.
set (_generators cpp)
if (ANDROID)
  list (APPEND _generators android)
elseif (CMAKE_GENERATOR STREQUAL "Xcode")
  list (APPEND _generators swift)
endif()

# Add step to generate sources. Lime files are specified below.
gluecodium_generate(mylibrary GENERATORS ${_generators})

# Gluecodium has plenty options which can be configured with target properties
# prfixed with `GLUECODIUM_`. To learn all the properties configure the project
# with `-DGLUECODIUM_PRINT_KNOWN_PROPERTIES=ON` parameter.
set_target_properties(mylibrary PROPERTIES
    # Not required, but highly desired Java annotations.
    GLUECODIUM_JAVA_NONNULL_ANNOTATION "androidx.annotation.NonNull"
    GLUECODIUM_JAVA_NULLABLE_ANNOTATION "androidx.annotation.Nullable"

    # Set prefix for java package.
    GLUECODIUM_JAVA_PACKAGE "com")

# This is the most convenient way to add lime files from which Gluecodium generates code.
set_property(TARGET mylibrary APPEND PROPERTY GLUECODIUM_LIME_SOURCES
              "${CMAKE_CURRENT_LIST_DIR}/lime/Calculator.lime")

if (CMAKE_GENERATOR STREQUAL "Xcode")
  # Make CMake to build framework.
  set_target_properties(mylibrary PROPERTIES
      FRAMEWORK TRUE
      XCODE_ATTRIBUTE_DEFINES_MODULE YES)

  if(ENABLE_APP AND IOS)
    add_subdirectory("ios")
  endif()
endif()
