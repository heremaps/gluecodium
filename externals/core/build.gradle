plugins {
    id "org.xtext.xtend" version "1.0.17"
}

ext.xtextVersion = '2.11.0'
ext.externalPath = "${projectDir}/../../../external/franca/plugins/org.franca.core/"

configurations {
    mwe2 {
        extendsFrom compile
        // There is a strange bug in gradle that the .pom xml files are added to the class
        // path, if this dependency is added.
        exclude module: 'org.eclipse.xtext.dependencies'
    }
}

dependencies {
    compile 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'
    compile 'org.eclipse.xpand:org.eclipse.xtend.typesystem.emf:+'
    compile 'org.eclipse.xpand:org.eclipse.xpand:+'
    compile 'org.apache.logging.log4j:log4j-core:+'
    compile 'commons-lang:commons-lang:+'
    compile 'commons-logging:commons-logging:1.2'
    compile 'org.osgi:org.osgi.core:+'
    compile 'org.eclipse.emf:org.eclipse.emf.mwe.core:+'
    compile "org.eclipse.xtext:org.eclipse.xtext.xbase.lib:${xtextVersion}"
    compile "org.eclipse.xtext:org.eclipse.xtext.ecore:${xtextVersion}"
    compile "org.eclipse.xtext:org.eclipse.xtext.generator:${xtextVersion}"
    compile "org.eclipse.equinox:common:+"

    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:+'
    xtextLanguages "org.eclipse.xtext:org.eclipse.xtext.ecore:${xtextVersion}"

    mwe2 'org.eclipse.emf:org.eclipse.emf.mwe2.launch:2.8.2'
    mwe2 "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
    mwe2 "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
    mwe2 "org.eclipse.xtend:org.eclipse.xtend.core:${xtextVersion}"
}


sourceSets {
    main {
        java {
            srcDirs "${externalPath}/model"
            srcDirs "${externalPath}/src"
            srcDirs "${externalPath}/src-gen"
        }

        // if these resources are not included, the xtext parsing will not work at runtime
        resources {
            srcDirs "${externalPath}/model"
            include "**/*.ecore"
            include "**/*.genmodel"
        }
    }
}

task generateXTextLanguage(type: JavaExec) {
    def mwe2File = 'src/org/franca/core/GenerateEmfSources.mwe2'

    workingDir = "${externalPath}"
    main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = configurations.mwe2
    inputs.file "${externalPath}/${mwe2File}"
    outputs.dir "${externalPath}/src-gen"
    outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()

    args += mwe2File
}

generateXtext.dependsOn(generateXTextLanguage)
clean.dependsOn(cleanGenerateXTextLanguage)
