plugins {
    id 'antlr'
}

dependencies {
    antlr "org.antlr:antlr4:4.9.1"
    api project(":lime-runtime")
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.10'
    implementation "org.antlr:antlr4-runtime:4.9.1"

    testImplementation files({ project(":lime-runtime").sourceSets.test.output })
    testImplementation 'io.mockk:mockk-dsl-jvm:1.12.3'
    testImplementation 'io.mockk:mockk:1.12.3'
    testImplementation 'junit:junit:4.13.2'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'com.here.gluecodium.antlr']
    outputDirectory = new File("$buildDir/generated-src/antlr/main/com/here/gluecodium/antlr")
}

compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

sourceSets {
    antlrGenerated {
        java {
            srcDir "${buildDir}/generated-src/antlr/main"
            compileClasspath = main.compileClasspath
            runtimeClasspath = main.runtimeClasspath
        }
    }
    main {
        java {
            compileClasspath += antlrGenerated.output
            runtimeClasspath += antlrGenerated.output
        }
    }
}

javadoc {
    exclude 'com/here/gluecodium/antlr/**'
}

apply from: "$rootDir/gradle/spotless.gradle"

spotless {
    format 'antlr', {
        target '**/*.g4'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}

project.ext.longName = 'Gluecodium LIME loader'
apply from: "$rootDir/gradle/publishing.gradle"
