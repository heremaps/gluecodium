/*
 * Copyright (C) 2016-2024 HERE Europe B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 * License-Filename: LICENSE
 */

plugins {
    id 'antlr'
}

dependencies {
    antlr libs.antlr
    api project(":lime-runtime")
    implementation libs.kotlin.stdlib
    implementation libs.antlr.runtime

    testImplementation files({ project(":lime-runtime").sourceSets.test.output })
    testImplementation libs.bundles.mockk
    testImplementation libs.junit
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'com.here.gluecodium.antlr']
    outputDirectory = new File("$buildDir/generated-src/antlr/main/com/here/gluecodium/antlr")
}

compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

sourceSets {
    antlrGenerated {
        java {
            srcDir "${buildDir}/generated-src/antlr/main"
            compileClasspath = main.compileClasspath
            runtimeClasspath = main.runtimeClasspath
        }
    }
    main {
        java {
            compileClasspath += antlrGenerated.output
            runtimeClasspath += antlrGenerated.output
        }
    }
}

javadoc {
    exclude 'com/here/gluecodium/antlr/**'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    dependsOn(tasks.withType(AntlrTask))
}

tasks.withType(Jar).configureEach {
    dependsOn(tasks.withType(AntlrTask))
}

apply from: "$rootDir/gradle/spotless.gradle"

spotless {
    format 'antlr', {
        target '**/*.g4'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}

project.ext.longName = 'Gluecodium LIME loader'
apply from: "$rootDir/gradle/publishing.gradle"
